buildscript {
    repositories {
        // These repositories are only for Gradle plugins, put any other repositories in the repository block further below
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'maven-publish'
    id 'java-library'
    id 'signing'
    id 'idea'
    id 'net.minecraftforge.gradle' version '6+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'

group = group_id
version = mod_version

java {
    archivesBaseName = mod_name
    toolchain.languageVersion = JavaLanguageVersion.of(17)
    //withJavadocJar()
    withSourcesJar()
}

minecraft {
    mappings channel: 'parchment', version: '2023.09.03-1.20.1'
    accessTransformer = file("src/main/resources/META-INF/coldsweat_at.cfg")
    runs {
        client {
            workingDirectory project.file('run')
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'coldsweat'
            mods {
                coldsweat {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'coldsweat'
            mods {
                coldsweat {
                    source sourceSets.main
                }
            }
        }

        gameTestServer {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'coldsweat'
            mods {
                coldsweat {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'cold_sweat', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            mods {
                cold_sweat {
                    source sourceSets.main
                }
            }
        }
    }
}

mixin {
    add sourceSets.main, "cold_sweat.mixin-refmap.json"
    config 'cold_sweat.mixin.json'
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        url 'https://www.cursemaven.com'
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        // location of the maven for Registrate and Flywheel
        name = 'tterrag maven'
        url = 'https://maven.tterrag.com'
    }

    maven {
        name = 'MinecraftForge'
        url = 'https://maven.minecraftforge.net/'
    }
    // Add KFF Maven repository
    maven {
        name = 'Kotlin for Forge'
        url = 'https://thedarkcolour.github.io/KotlinForForge/'
    }
    maven {
        name = "Valkyrien Skies Internal"
        url = 'https://maven.valkyrienskies.org'
    }
    maven {
        // Shedaniel's maven (Architectury API)
        url = "https://maven.architectury.dev"
        content {
            includeGroup "dev.architectury"
        }
    }
    maven {
        // saps.dev Maven (KubeJS and Rhino)
        url = "https://maven.saps.dev/minecraft"
        content {
            includeGroup "dev.latvian.mods"
        }
    }
    mavenCentral()
}

dependencies {
    implementation 'com.github.t9t.minecraft-rcon-client:minecraft-rcon-client:1.0.0'
    minecraft 'net.minecraftforge:forge:1.20.1-47.3.11'
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    compileOnly fg.deobf("curse.maven:werewolves-become-a-beast-417851:4710810")
    compileOnly fg.deobf("curse.maven:vampirism-233029:4783000")
    compileOnly fg.deobf("curse.maven:weather-storms-tornadoes-237746:4931290")
    compileOnly fg.deobf("curse.maven:coroutil-237749:4995502")
    compileOnly fg.deobf("curse.maven:iceberg-520110:4953572")
    //compileOnly fg.deobf("curse.maven:armorunderwear-310383:4383256") // no 1.19.2 version
    //compileOnly fg.deobf("curse.maven:carrots-311327:4382778") // no 1.19.2 version
    compileOnly("com.tterrag.registrate:Registrate:MC1.20-1.3.3")

    implementation fg.deobf("curse.maven:create-328085:4762216")
    implementation fg.deobf("curse.maven:curios-309927:5367944-sources-5367945")
    implementation fg.deobf("curse.maven:thirst-was-taken-679270:5568703")
    implementation fg.deobf("curse.maven:glitchcore-955399:5787839")
    implementation fg.deobf("curse.maven:serene-seasons-291874:5790653")
    implementation fg.deobf("curse.maven:spoiled-364918:4803042")

    compileOnly         ("org.valkyrienskies.core:api:${valk_skies_api_version}")
    compileOnly         ("org.valkyrienskies.core:impl:${valk_skies_api_version}")
    compileOnly         ("org.valkyrienskies.core:api-game:${valk_skies_api_version}")
    compileOnly         ("org.valkyrienskies.core:util:${valk_skies_api_version}")
    compileOnly fg.deobf("org.valkyrienskies:valkyrienskies-120-forge:2.3.0-beta.5+bcda04a482")
    compileOnly fg.deobf("curse.maven:eureka-ships-654384:5321630")

    implementation fg.deobf("dev.latvian.mods:kubejs-forge:${kubejs_version}")
    implementation fg.deobf("dev.latvian.mods:rhino-forge:${rhino_version}")
    implementation fg.deobf("dev.architectury:architectury-forge:${architectury_version}")

    implementation 'thedarkcolour:kotlinforforge:4.10.0'

    runtimeOnly fg.deobf("curse.maven:spark-361579:4738952")
    runtimeOnly fg.deobf("curse.maven:biomes-o-plenty-220318:4764804")
    runtimeOnly fg.deobf("curse.maven:terrablender-563928:5378180")
    //runtimeOnly fg.deobf("curse.maven:legendary-tooltips-532127:4662781")
    //runtimeOnly fg.deobf("curse.maven:prism-lib-638111:4650325")
    runtimeOnly fg.deobf("curse.maven:alexs-mobs-426558:4959531")
    runtimeOnly fg.deobf("curse.maven:citadel-331936:5075402")
    //runtimeOnly fg.deobf("curse.maven:irons-spells-855414:5061897")
    //runtimeOnly fg.deobf("curse.maven:geckolib-388172:5188427")
    runtimeOnly fg.deobf("curse.maven:playeranimator-658587:4587214")
    runtimeOnly fg.deobf("curse.maven:caelus-308989:4581481")
    runtimeOnly fg.deobf("curse.maven:comforts-276951:5068863")
    runtimeOnly fg.deobf("curse.maven:packet-fixer-689467:5416166")

    runtimeOnly fg.deobf("curse.maven:attributefix-280510:4844658")
    runtimeOnly fg.deobf("curse.maven:customskinloader-286924:5497382")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = 'coldsweat'
            pom {
                name = 'Cold Sweat'
                description = 'A Minecraft temperature mod with a powerful framework'
                url = 'https://github.com/Momo-Softworks/Cold-Sweat'
                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }
                developers {
                    developer {
                        id = 'mikul'
                        name = 'Mikul'
                    }
                }
                /*scm = {
                    connection = 'scm:git:git://github.com/Momo-Softworks/Cold-Sweat.git'
                    developerConnection = 'scm:git:ssh://github.com/Momo-Softworks/Cold-Sweat.git'
                    url = 'https://github.com/Momo-Softworks/Cold-Sweat'
                }*/
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("SONATYPE_USERNAME")
                password = System.getenv("SONATYPE_PASSWORD")
            }
        }
    }
}

// Example for how to get properties into the manifest for reading at runtime.
jar {
    manifest {
        attributes([
                "Specification-Title"     : "cold_sweat",
                "Specification-Vendor"    : "Mikul",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "Mikul",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "TweakerClass"            : "org.spongepowered.asm.launch.MixinTweaker",
                "TweakOrder"              : 0,
                "MixinConfigs"            : "cold_sweat.mixin.json"
        ])
    }
}

jar.finalizedBy('reobfJar')

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
